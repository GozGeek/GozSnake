0 REM SAVE"D:GSNAKE.BAS":LIST"D:GSNAKE.LST"
1 REM BY GOZ 2022-09-27
1000 REM SETUP
1010 GRAPHICS 0:POKE 752,1:? 
1015 SCREEN=PEEK(88)+PEEK(89)*256
1017 GOTO 10000
1020 PX=19:PY=11:L=2:FX=INT(RND(0)*37)+1:FY=INT(RND(0)*22)+1
1025 DIM PXH$(1024),PYH$(1024)
1027 PXH$(1,1)=CHR$(PX):PYH$(1,1)=CHR$(PY):PHL=2
1030 POSITION PX,PY:? #6;"X";
1040 POSITION FX,FY:? #6;"#";
1050 POKE 764,32
1200 KEY=PEEK(764)
1202 REM SOUND 0,243,2,8:FOR TMP=0 TO 10:NEXT TMP:SOUND 0,0,0,0:FOR TMP=0 TO 10:NEXT TMP
1204 REM FOR TMP=1 TO 20:NEXT TMP
1205 PPX=PX:PPY=PY
1210 IF KEY<>63 AND KEY<>23 AND KEY<>32 AND KEY<>34 THEN KEY=PKEY
1220 IF KEY=63 THEN PY=PY-1
1230 IF KEY=23 THEN PY=PY+1
1240 IF KEY=32 THEN PX=PX-1
1250 IF KEY=34 THEN PX=PX+1
1260 IF PX=FX AND PY=FY THEN GOSUB 2000:GOTO 1280
1270 IF PEEK(SCREEN+PX+PY*40)<>0 THEN 5000
1280 POSITION PX,PY:? #6;"X";
1300 PHL=PHL+1:PXH$(PHL,PHL)=CHR$(PX):PYH$(PHL,PHL)=CHR$(PY)
1310 POSITION ASC(PXH$(1,1)),ASC(PYH$(1,1)):? #6;" ";
1320 PXH$=PXH$(2):PYH$=PYH$(2)
1350 PHL=L
1490 POSITION 15,0:? #6;"SCORE:";:POSITION 25,0:? #6;PHL;
1499 PKEY=KEY
1500 GOTO 1200
2000 P=PHL-L+1
2010 POSITION ASC(PXH$(P,P)),ASC(PYH$(P,P)):? #6;" ";
2020 L=L+1
2030 FX=INT(RND(0)*37)+1
2040 FY=INT(RND(0)*22)+1
2045 IF PEEK(SCREEN+FX+FY*40)<>0 THEN 2030
2050 POSITION FX,FY:? #6;"#";
2200 RETURN 
3000 ? PEEK(764):GOTO 3000
5000 POSITION 16,12:? #6;"YOU LOSE!"
5999 GOTO 5999
10000 REM DRAW BORDER
10010 REM SCREEN IS SCREEN LOCATION
10020 POSITION 0,1:? #6;"";
10030 POSITION 39,1:? #6;"";
10040 POSITION 0,23:? #6;"";
10050 POKE SCREEN+959,67
10060 FOR X=1 TO 38
10070 POSITION X,1:? #6;"";
10080 POSITION X,23:? #6;"";
10090 NEXT X
10100 FOR Y=2 TO 22
10110 POSITION 0,Y:? #6;"|";
10120 POKE SCREEN+39+(Y*40),124
10125 REM POSITION 39,Y:? #6;"|";
10130 NEXT Y
12000 GOTO 1020
